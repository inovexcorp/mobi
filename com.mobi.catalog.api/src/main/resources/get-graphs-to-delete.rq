PREFIX mcat: <http://mobi.com/ontologies/catalog#>
PREFIX prov: <http://www.w3.org/ns/prov#>

SELECT DISTINCT ?graph WHERE {
  {
    ?recordId a mcat:VersionedRDFRecord ;
  		mcat:branch ?graph .
    ?graph a mcat:Branch .
  }
  UNION
  {
    ?recordId mcat:branch ?branch .
    ?branch mcat:head ?graph .
    ?graph a mcat:Commit .
  }
  UNION
  {
    ?recordId mcat:branch ?branch .
    ?branch mcat:head ?headCommit .
    ?headCommit a mcat:Commit ;
        (mcat:baseCommit | mcat:branchCommit | mcat:auxiliaryCommit)+ ?graph .
    ?graph a mcat:Commit .
  }
  UNION
  {
    ?recordId mcat:branch ?branch .
    ?branch mcat:head ?headCommit .
    ?headCommit a mcat:Commit ;
        (prov:generated | prov:influenced | prov:hadPrimarySource | prov:wasDerivedFrom | mcat:mergeDisplayRevision | mcat:forwardMergeBaseRevision | mcat:forwardMergeAuxRevision | mcat:initialRevision | mcat:masterMergeIntoBranchRevision) ?revision .
    ?revision a mcat:Revision ;
        (mcat:additions | mcat:deletions) ?graph .
  }
  UNION
  {
    ?recordId mcat:branch ?branch .
    ?branch mcat:head ?headCommit .
    ?headCommit a mcat:Commit ;
        (mcat:baseCommit | mcat:branchCommit | mcat:auxiliaryCommit)+ ?commit .
    ?commit a mcat:Commit ;
        (prov:generated | prov:influenced | prov:hadPrimarySource | prov:wasDerivedFrom | mcat:mergeDisplayRevision | mcat:forwardMergeBaseRevision | mcat:forwardMergeAuxRevision | mcat:initialRevision | mcat:masterMergeIntoBranchRevision) ?revision .
    ?revision a mcat:Revision ;
      (mcat:additions | mcat:deletions) ?graph .
  }
  UNION {
    ?recordId mcat:masterBranch ?branch .
    ?branch a mcat:MasterBranch;
        mcat:headGraph ?graph .
  }
}