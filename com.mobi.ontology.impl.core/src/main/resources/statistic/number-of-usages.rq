PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX catalog: <http://mobi.com/ontologies/catalog#>

# USAGE: BIND (<RecordId> as ?record)
SELECT
  (COUNT(DISTINCT ?numberOfUsage) AS ?numberOfUsages)
WHERE {
  GRAPH ?record {
    ?record a catalog:Record;
      catalog:masterBranch ?masterBranch .
  }
  GRAPH ?masterBranch {
    ?masterBranch a catalog:MasterBranch;
      catalog:headGraph ?headGraph .
  }
  GRAPH ?headGraph {
    ?targetOntology a owl:Ontology .
  }
  GRAPH ?numberOfUsage {
    ?numberOfUsage a catalog:Record;
      catalog:masterBranch ?otherMasterBranch .
  }
  GRAPH ?otherMasterBranch {
    ?otherMasterBranch a catalog:MasterBranch;
      catalog:headGraph ?otherHeadGraph .
  }
  GRAPH ?otherHeadGraph {
    ?ontOther a owl:Ontology;
      owl:imports ?targetOntology .
  }
}