PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX skos-xl: <http://www.w3.org/2008/05/skos-xl#>

SELECT
?entity
(GROUP_CONCAT(DISTINCT ?enPrefNames;separator="�") as ?en_pref_names_array)
(GROUP_CONCAT(DISTINCT ?prefNames;separator="�") as ?pref_names_array)
(GROUP_CONCAT(DISTINCT ?names;separator="�") as ?names_array)
WHERE {
    {
        ?entity a ?type .
    	FILTER(isIRI(?entity))

        ?entity
        rdfs:label|
        dcterms:title|
        dc:title|
        skos:prefLabel|
        skos:altLabel|
        (skos-xl:prefLabel/skos-xl:literalForm)|
        (skos-xl:altLabel/skos-xl:literalForm)
        ?names .
    }

    UNION {
        ?entity a ?type .
    	FILTER(isIRI(?entity))

        OPTIONAL {
            ?entity rdfs:label ?label .
        }

        OPTIONAL {
            ?entity dcterms:title ?dctTitle .
        }

        OPTIONAL {
            ?entity dc:title ?dceTitle .
        }

        OPTIONAL {
            ?entity skos:prefLabel ?prefLabel .
        }

        OPTIONAL {
            ?entity skos:altLabel ?altLabel .
        }

        OPTIONAL {
            ?entity (skos-xl:prefLabel/skos-xl:literalForm) ?skosXLPrefLiteralForm .
        }

        OPTIONAL {
            ?entity (skos-xl:altLabel/skos-xl:literalForm) ?skosXLAltLiteralForm .
        }

        BIND(COALESCE(
            ?label,
            ?dctTitle,
            ?dceTitle,
            ?prefLabel,
            ?altLabel,
            ?skosXLPrefLiteralForm,
            ?skosXLAltLiteralForm) as ?prefNames)

        FILTER(BOUND(?prefNames))

        OPTIONAL {
            ?entity ?p ?prefNames
            FILTER(LANGMATCHES(LANG(?prefNames), "EN"))
            BIND(?prefNames as ?enPrefNames)
        }
    }
} GROUP BY ?entity HAVING BOUND(?entity)
